;---------------------------------------------------
; Up Scroll Test
; by C64portal.pl and Retronavigator.com
;
; Proof of concept based on the disussion on EAB forum
; started in 22.09.2019
;
;---------------------------------------------------


BitMap 0,320,468,4

LoadBitMap 0,"tiles.iff",0

dlist=0
BLITZ
InitCopList dlist,44,16*14,$10014,8,16,-4

CreateDisplay dlist
DisplayPalette 0,0

;---------------------------------------------------
; init tileset

sh=0
For j=0 To 26-1
    For i=0 To 20-1
        GetaShape sh,i*16,j*16,16,16
           sh+1
    Next i
Next j


;---------------------------------------------------

#TILESIZE=16
#SCRHEIGHT=14
#MAPWIDTH=24
#MAPHEIGHT=100

BitMap 0,384,(2*#SCRHEIGHT+2)*#TILESIZE,4

tile.w =0
t.w    =#MAPHEIGHT*#MAPWIDTH-1

For j=1 To #SCRHEIGHT
  For i=0 To #MAPWIDTH-1
    tile=Peek(?map+(t*2))
    Block tile,(#MAPWIDTH-1-i)*#TILESIZE,(28-j)*#TILESIZE
    t-1
  Next i
Next j

dx=(384/2)-160

dy.l  = (#SCRHEIGHT*#TILESIZE)+16
dyp.b =0

mp1.w =(#SCRHEIGHT*2)+1
mp2.w =#SCRHEIGHT

t.w   =(#MAPHEIGHT-#SCRHEIGHT)*#MAPWIDTH-1

;----------------------------------------------
.mainloop:

While (Joyb(0)=0)
  VWait 3
  DisplayBitMap dlist,0,dx,dy

  If dy=0
    dy.l  = (#SCRHEIGHT*#TILESIZE)+16
  Else
      dy-1
  EndIf

  If dyp=0     ; -------- every 15 line draw new tile line
    dyp=15

       For i=0 To #MAPWIDTH-1
        tile=Peek(?map+(t*2))
        xx.w=(#MAPWIDTH-1-i)*16
        y1=(mp1)*#TILESIZE
        y2=(mp2)*#TILESIZE
        Block tile,xx,y1
        Block tile,xx,y2
        t-1
      Next i

      If mp1=0
        mp1=(#SCRHEIGHT*2)+0
      Else
        mp1-1
      EndIf

      If mp2=0
        mp2=(#SCRHEIGHT*2)+1
      Else
        mp2-1
      EndIf

  Else
    dyp-1
  EndIf


Wend

End


;---------------------------------------------------

.map:
IncBin "lvl1.csv.asm.bin"




